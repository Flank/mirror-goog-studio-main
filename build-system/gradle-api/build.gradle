apply plugin: 'java'
apply plugin: 'jacoco-tools-base'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

configurations {
    provided
}

dependencies {
    compile project(':base:builder-test-api')
    compile libs.guava
    compile libs.kotlin_stdlib

    provided gradleApi()
    provided project(':base:builder-model') // Only present for legacy reasons.

    testCompile libs.junit
    testCompile project(':base:testutils')
    testCompile gradleApi()
    testCompile project(':base:builder-model')
    testCompile libs.mockito_core
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

javadoc {
  classpath += configurations.provided
  options.links("http://docs.oracle.com/javase/8/docs/api/", "https://docs.gradle.org/current/javadoc/")
}

task javadocZip(type: Zip) {
  dependsOn javadoc
  from javadoc.destinationDir
  baseName 'javadoc'
}

group = 'com.android.tools.build'
archivesBaseName = 'gradle-api'
version = rootProject.ext.buildVersion

project.ext.pomName = 'Android Gradle API'
project.ext.pomDesc = 'APIs to customize Android Gradle Builds'

apply from: "$rootDir/buildSrc/base/publish.gradle"
apply from: "$rootDir/buildSrc/base/javadoc.gradle"

dokka {
    outputFormat = 'dac'
    jdkVersion = 8

    includeNonPublic = false
    skipDeprecated = false
    skipEmptyPackages = false
    noJdkLink = true

    dacRoot = "/reference/tools"
    classpath = [new File('$rootDir/external/gradle/gradle-5.4.1-bin.zip')]

    sourceRoot {
        path = "$projectDir/src/main/java/com/android/build/*"
        platforms = ["JVM"]
    }
    externalDocumentationLink {
        // Root URL of the generated documentation to link with. The trailing slash is required!
        url = new URL("https://docs.gradle.org/current/javadoc/")
        url = new URL("http://docs.oracle.com/javase/8/docs/api/")
    }
}

task dokkaZip(type: Zip) {
    dependsOn dokka
    from dokka.outputDirectory
    destinationDirectory = layout.projectDirectory.dir("$rootDir/../out/dist")
    archiveFileName = "documentation.zip"
}