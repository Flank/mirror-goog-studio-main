import java.time.LocalDate
import java.time.format.DateTimeFormatter

apply plugin: 'java'
apply plugin: 'clone-artifacts'
apply plugin: 'idea'
apply plugin: 'jacoco-tools-base'

// Ordinarily, we depend on an old version of Gradle and let projects
// like gradle-core link against it.  This way, we maintain backward
// compatibility for gradle-core.  This project, however, uses newer
// Gradle features, so we link against Gradle version $gradleVersion
// instead.  This linking override works by prepending the extracted
// $gradleVersion jar to our classpath when compiling this project.
// The extraction happens via the top-level `init' task, which we do
// *not* run automatically.

// If you try to compile this project and see errors about
// non-existent Gradle APIs, you haven't run top-level `init'
// recently enough.

String gradleVersion = "3.3"
File gradleApi = rootProject.file('external/gradle/gradle-api-' + gradleVersion + '.jar')
assert gradleApi.exists()
dependencies {
    compile files(gradleApi)
    compile libs.groovy
    compile project(':base:gradle-core')

    testCompile libs.junit
    testCompile project(':base:project-test-lib')
}

task setupGradleInIde {
}

group = 'com.android.tools.build'
archivesBaseName = 'gradle-experimental'
version = rootProject.ext.experimentalVersion

project.ext.pomName = 'Gradle Plug-in for Android Using Component Model'
project.ext.pomDesc = 'Gradle plug-in to build Android applications.'

apply from: "$rootDir/buildSrc/base/publish.gradle"
apply from: "$rootDir/buildSrc/base/bintray.gradle"

jar.manifest.attributes("Plugin-Version": version)
jar.manifest.attributes("Inception-Date": DateTimeFormatter.ISO_LOCAL_DATE.format(LocalDate.now()));

def generated = new File("${project.buildDir}/generated/resources")
sourceSets.main.resources.srcDir generated
task copyVersionProperties(type: Copy) {
    from project.file("$rootDir/buildSrc/base/version.properties")
    into project.file("$generated/com/android/build/gradle/model")
}

tasks.compileJava.dependsOn copyVersionProperties

test {
    environment("CUSTOM_REPO", rootProject.file("../out/repo"))

    testLogging {
        events "failed"
    }

    maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

javadoc {
    exclude     "**/internal/**"

    options.docTitle "Gradle Plugin for Android"
    options.header ""
    options.footer "Copyright (C) 2012 The Android Open Source Project"
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier  'javadoc'
    from        javadoc.destinationDir

}

// Only package JavaDoc if using --init-script=buildSrc/base/release.gradle
if (project.hasProperty("release")) {
  artifacts {
    archives javadocJar
  }
}

check.dependsOn javadocJar, javadoc
