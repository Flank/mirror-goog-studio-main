apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.latestCompileSdk
    buildToolsVersion = rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.latestCompileSdk

        externalNativeBuild {
            if (!project.hasProperty("ndkBuild")) {
                cmake {
                    arguments "-DANDROID_STL=c++_shared"
                }
            }
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        if (project.hasProperty("ndkBuild")) {
            ndkBuild {
                path "src/main/cpp/Android.mk"
            }
        } else {
            cmake {
                path "src/main/cpp/CMakeLists.txt"
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"
    implementation project(':curl')
    implementation project(':jsoncpp')
    implementation project(':openssl')
}
