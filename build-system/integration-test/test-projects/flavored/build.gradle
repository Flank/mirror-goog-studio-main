apply from: "../commonHeader.gradle"
buildscript { apply from: "../commonBuildScript.gradle" }
apply from: "../commonLocalRepo.gradle"

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.latestCompileSdk
    buildToolsVersion = rootProject.buildToolsVersion

    testBuildType = "staging"

    defaultConfig {
        minSdkVersion rootProject.supportLibMinSdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions 'foo'
    productFlavors {
        f1 {
            applicationId = "com.android.tests.flavored.f1"
            versionName = "1.0.0-f1"
            ext.buildType = "F1PROD"
        }
        f2 {
            applicationId = "com.android.tests.flavored.f2"
            versionName = "1.0.0-f2"
            ext.buildType = "F2PROD"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = ".D"
        }
        staging {
            applicationIdSuffix = ".staging"
            versionNameSuffix = ".S"
            signingConfig signingConfigs.debug
        }
    }

    // This is not part of the test per se, it tests that adding dynamic properties on the
    // flavor declaration can be retrieved later.
    applicationVariants.all { variant ->
        assert variant.productFlavors.size() == 1
        def buildType = variant.productFlavors[0].buildType
        if (!"F1PROD".equals(buildType)
            && !"F2PROD".equals(buildType)) {
            throw new RuntimeException("Invalid extension property value ${buildType}")
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${project.testSupportLibVersion}"
    androidTestImplementation "com.android.support.test:rules:${project.testSupportLibVersion}"
}