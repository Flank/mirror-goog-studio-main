apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.latestCompileSdk
    defaultConfig {
        minSdkVersion 23
        //noinspection ExpiringTargetSdkVersion,ExpiredTargetSdkVersion
        targetSdkVersion rootProject.latestCompileSdk
    }
}

repositories {
    maven { url '../localRepo' }
}

dependencies {
    // Basic dependency for MainActivity
    implementation "com.android.support:appcompat-v7:${rootProject.supportLibVersion}"

    // Example third-party library that should be jetified and its support library dependencies
    // should be replaced with AndroidX
    implementation 'com.example.androidlib:androidLibToRefactor:1.0'

    // Add these dependencies as regression test for https://issuetracker.google.com/78202788
    implementation 'android.arch.lifecycle:common:1.1.0'
    implementation 'android.arch.persistence.room:runtime:1.0.0'

    // Add this dependency as regression test for https://issuetracker.google.com/78477288
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Add this dependency as regression test for https://issuetracker.google.com/78552861
    // This is a fake dependency in ../localRepo since we do not have it in prebuilts
    implementation 'com.google.android.gms:play-services-basement:15.0.0'

    // Add this dependency as regression test for https://issuetracker.google.com/78144836
    // (Add it to implementation instead of androidTestImplementation so it's tested by the assemble
    // task.)
    // TODO: Comment out for now to wait for the AndroidX version of Android testing support library
    // to be available on GMaven. Once it is available, remove the comment.
    //implementation "com.android.support.test:runner:${rootProject.testSupportLibVersion}"

    // Add these dependencies as regression test for https://issuetracker.google.com/78683535
    compileOnly "com.android.support:support-annotations:${rootProject.supportLibVersion}"
    annotationProcessor 'com.example.annotationprocessor:annotationProcessor:1.0'
}

// TODO (AGP): Many AndroidX dependencies do not have version 1.0.0-alpha3 available in prebuilts
// yet, so we need to correct their versions here for testing only. In practice, the users will
// still get version 1.0.0-alpha3 of these libraries from GMaven. This workaround needs to be
// removed once the libraries are added to prebuilts.
configurations.all {
    resolutionStrategy.eachDependency {
        if ((requested.group.startsWith('androidx')
                        || requested.group == 'com.google.android.material')
                && requested.version == '1.0.0-alpha3') {
            useVersion '1.0.0-alpha1'
        }
    }
}

// Some (transitive) dependencies may not be available in prebuilts. When Jetifier is disabled, we
// need to adjust their versions here so that the integration tests can find them in prebuilts. This
// is only for testing, in practice the users don't need to do this.
configurations.all {
    resolutionStrategy.eachDependency {
        def requested = requested.group + ":" + requested.name
        if (requested == 'com.android.support:preference-v7'
                || requested == 'com.android.support:support-annotations'
                || requested == 'com.android.support:support-v4') {
            useVersion rootProject.supportLibVersion
        }
    }
}
