
apply from: "../commonHeader.gradle"
buildscript { apply from: "../commonBuildScript.gradle" }
apply from: "../commonLocalRepo.gradle"

apply plugin: 'com.android.application'


dependencies {
    api "com.android.support:support-v4:${rootProject.supportLibVersion}"
    api "com.google.android.gms:play-services-base:${rootProject.playServicesVersion}"

    debugApi "com.android.support:support-v13:${rootProject.supportLibVersion}"
    releaseApi "com.android.support:support-v13:${rootProject.supportLibVersion}"

    // hamcrest-library depends on hamcrest-core, both provide a /LICENSE.txt file
    // which used to cause packaging conflict. We added a special case for license files.
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${project.testSupportLibVersion}"
    androidTestImplementation "com.android.support.test:rules:${project.testSupportLibVersion}"
}

android {
    compileSdkVersion rootProject.latestCompileSdk
    buildToolsVersion = rootProject.buildToolsVersion

    testBuildType "debug"

    defaultConfig {
        versionCode 12
        versionName "2.0"
        minSdkVersion 16
        //noinspection ExpiringTargetSdkVersion,ExpiredTargetSdkVersion
        targetSdkVersion 16

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "size", "medium"

        testHandleProfiling false

        buildConfigField "boolean", "DEFAULT", "true"
        buildConfigField "String", "FOO", "\"foo\""
        buildConfigField "String", "FOO", "\"foo2\""

        resValue "string", "foo", "foo"

        resConfig "en"
        resConfigs "hdpi"

        manifestPlaceholders = [ "someKey":12 ]
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            testCoverageEnabled true

            buildConfigField "String", "FOO", "\"bar1\""
            buildConfigField "String", "FOO", "\"bar\""

            resValue "string", "foo", "foo2"
        }
    }

    aaptOptions {
        noCompress 'txt'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    adbOptions {
          timeOutInMs 5000 // 5 seconds in ms.
          installOptions "-d","-t"
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // turn off checking the given issue id's
        disable 'TypographyFractions','TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded','RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("lint-report.html")
        // Reduce severity of this check to just informational
        informational 'LogConditional'
        // Run all lint checks on all test sources, not just production code
        checkTestSources true
        // Run all lint checks on generated sources
        checkGeneratedSources true
    }

    // Override the versionCode of the release version
    applicationVariants.all { variant ->
        print variant
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                print("Customizing " + this)
                setVersionCodeOverride(13)
                print("Done with " + this)
            }
        }
    }
}
