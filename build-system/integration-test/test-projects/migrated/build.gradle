apply from: "../commonHeader.gradle"
buildscript { apply from: "../commonBuildScript.gradle" }

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.latestCompileSdk
    buildToolsVersion = rootProject.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.supportLibMinSdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest {
                // there's only ever one file so srcFile replaces it.
                srcFile 'AndroidManifest.xml'
            }
            java {
                // writing:
                //    srcDir 'src'
                // would *add* to the default folder so we use a different syntax
                srcDirs = ['src']
                exclude 'some/unwanted/packageName/**'
            }
            res {
                srcDirs = ['res']
            }
            assets {
                srcDirs = ['assets']
            }
            resources {
                srcDirs = ['src']
            }
            aidl {
                srcDirs = ['src']
            }
            renderscript {
                srcDirs = ['src']
            }
        }

        // this moves src/androidTest to tests so all folders follow:
        // tests/java, tests/res, tests/assets, ...
        // This is a *reset* so it replaces the default paths
        androidTest.setRoot('tests')

        // Could also be done with:
        //main.manifest.srcFile 'AndroidManifest.xml'
        //main.java.srcDir 'src'
        //main.res.srcDir 'res'
        //main.assets.srcDir 'assets'
        //main.resources.srcDir 'src'
        //androidTest.java.srcDir 'tests/src'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${project.testSupportLibVersion}"
    androidTestImplementation "com.android.support.test:rules:${project.testSupportLibVersion}"
}