apply from: "../commonHeader.gradle"
buildscript { apply from: "../commonBuildScriptExperimental.gradle", to: buildscript }


apply plugin: 'com.android.model.application'

model {
    android.config {
        compileSdkVersion 19
        buildToolsVersion = rootProject.ext.buildToolsVersion

        defaultConfig {
            // This actual the app version code. Giving ourselves 1,000,000 values
            versionCode = 123
        }

        // make per-variant version code
        applicationVariants.all { variant ->
            // get the single flavor
            def flavorVersion = variant.productFlavors.get(0).versionCode

            // set the composite code
            variant.mergedFlavor.versionCode = flavorVersion * 1000000 + defaultConfig.versionCode
        }
    }
    android.buildTypes {
        afterEach {
            if (name == "debug") {
                isJniDebuggable = true
            }
        }
    }

    android.ndk {
        moduleName = "sanangeles"
        CFlags = "-DDISABLE_IMPORTGL"
        ldLibs.create { value = "GLESv1_CM" }
        ldLibs.create { value = "dl" }
        ldLibs.create { value = "log" }
        stl = "stlport_static"
        toolchain = "clang"
    }

    android.productFlavors {
        create {
            name = "x86"
            ndkConfig.abiFilters.create { value = "x86" }
            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
        }
        create {
            name = "arm"
            ndkConfig.abiFilters.create { value = "armeabi-v7a" }
            versionCode = 2
        }
        create {
            name = "mips"
            ndkConfig.abiFilters.create { value = "mips" }
            versionCode = 1
        }
        create {
            name = "fat"
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

}
