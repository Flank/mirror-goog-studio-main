apply plugin: 'java'
apply plugin: 'clone-artifacts'
apply plugin: 'jacoco'

dependencies {
    compile project(':base:builder-model')
    compile project(':base:builder-test-api')

    compile project(':base:sdklib')
    compile project(':base:sdk-common')
    compile project(':base:common')
    compile project(':base:manifest-merger')
    compile project(':base:ddmlib')

    compile project(':base:jack:jack-api')
    compile project(':base:jack:jill-api')

    compile project(':analytics-library:protos')
    compile project(':analytics-library:shared')
    compile project(':analytics-library:tracker')

    compile 'com.squareup:javawriter:2.5.0'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.48'
    compile 'org.bouncycastle:bcprov-jdk15on:1.48'
    compile 'org.ow2.asm:asm:5.0.4'
    compile 'org.ow2.asm:asm-tree:5.0.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.google.truth:truth:0.28'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.1.5'
    testCompile project(':base:testutils')
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

group = 'com.android.tools.build'
archivesBaseName = 'builder'
version = rootProject.ext.buildVersion

project.ext.pomName = 'Android Builder library'
project.ext.pomDesc = 'Library to build Android applications.'

apply from: "$rootDir/buildSrc/base/publish.gradle"
apply from: "$rootDir/buildSrc/base/bintray.gradle"
apply from: "$rootDir/buildSrc/base/javadoc.gradle"

def generated = new File("${project.buildDir}/generated/resources")

sourceSets {
    main {
        resources {
            srcDir generated
        }
    }
}

task copyVersionProperties(type: Copy) {
    from project.file("$rootDir/buildSrc/base/version.properties")
    into project.file("$generated/com/android/builder")
}

tasks.compileJava.dependsOn copyVersionProperties

jar.manifest.attributes("Builder-Version": version)

configurations {
    provided
    sourcesProvided
}

dependencies {
    provided(project(':base:profile')) {
        transitive = false
    }
    sourcesProvided(project(path:':base:profile', configuration:'sourcesOnly')) {
        transitive = false
    }
}

sourceSets.main.compileClasspath += [configurations.provided]
sourceSets.test.runtimeClasspath += [configurations.provided]
tasks.compileJava.dependsOn(configurations.provided)
tasks.sourcesJar.dependsOn(configurations.sourcesProvided)

tasks.jar {
    from({zipTree(configurations.provided.singleFile)})
}
tasks.sourcesJar {
    from({zipTree(configurations.sourcesProvided.singleFile)})
}
javadoc {
    classpath += configurations.provided
}
