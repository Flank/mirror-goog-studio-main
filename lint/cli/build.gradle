apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'sdk-java-lib'
apply plugin: 'application'

mainClassName = 'com.android.tools.lint.Main'
applicationDefaultJvmArgs = ["-Xmx1024m"]

group = 'com.android.tools.lint'
archivesBaseName = 'lint'
version = rootProject.ext.baseVersion

dependencies {
    compile project(':base:lint-checks')
    compile 'org.eclipse.jdt.core.compiler:ecj:4.5.1'
}

sourceSets {
    main.resources.srcDir 'src/main/java'
    test.resources.srcDir 'src/test/java'
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

sdk {
/*    common {
        item(new File(buildDir, "libs")) {
            into "lib"
            notice null
        }
    }*/
    common(mac, linux) {
        item(new File(buildDir, "scripts/lint")) {
            into "bin"
            notice null
        }
    }
    windows {
        item(new File(buildDir, "scripts/lint.bat")) {
            into "bin"
            notice null
        }
    }
}

sdkJar.dependsOn(startScripts)

// configure the manifest of the sdkJar task.
sdkJar.manifest.attributes("Main-Class": "com.android.tools.lint.Main")

project.ext.pomName = 'Android Lint Tool'
project.ext.pomDesc = 'Lint tools. Both a Command line tool and a library to add lint features to other tools'

apply from: "$rootDir/buildSrc/base/publish.gradle"
apply from: "$rootDir/buildSrc/base/bintray.gradle"
apply from: "$rootDir/buildSrc/base/javadoc.gradle"
