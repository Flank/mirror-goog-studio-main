apply plugin: 'java'
apply plugin: 'org.anarres.jarjar'

group = 'com.android.tools'

archivesBaseName = 'profilers-transform'

buildscript {
    dependencies {
        classpath 'org.anarres.jarjar:jarjar-gradle:1.0.0'
    }
}

configurations {
    repackage
    support
    supportslim
}

jarjar.repackage('asm') {
    destinationName = "profler-asm.jar"
    from configurations.repackage
    classRename 'org.objectweb.asm.**', 'com.android.tools.profiler.asm.@1'
}

dependencies {
    repackage('org.ow2.asm:asm:5.0.4') { transitive = false }

    support project(":supportlib")
    supportslim project(path: ":supportlib", configuration: "slim")

    compile files(asm.outputs)
}

jar {
    from asm.outputs.files.collect { zipTree(it) }
    from(configurations.support) {
        into 'dependencies'
    }
}

// Creates a jar that does not include the agentlib for post-O scenario.
// TODO this can be removed once post-O no longer requires supportlib
task(slimJar, dependsOn: 'classes', type: Jar) {
    appendix 'slim'
    from sourceSets.main.output
    from asm.outputs.files.collect { zipTree(it) }
    from(configurations.supportslim) {
        into 'dependencies'
    }
}

artifacts {
   archives slimJar
}
