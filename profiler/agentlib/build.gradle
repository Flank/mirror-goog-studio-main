/*
 * Copyright (C) 2015 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'com.android.tools'
archivesBaseName = 'studio-agent-lib'

File androidJar = file('../../../../prebuilts/sdk/23/android.jar');
if (!androidJar.exists()) {
    // If not there, we're in one of the AOSP release branches where the
    // repo configuration does not include the whole prebuilts repository.
    // Fetch the SDK via $ANDROID_HOME instead.
    File jar = file(System.env.ANDROID_HOME + '/platforms/android-23/android.jar');
    if (jar.exists()) {
        androidJar = jar
    } else {
        throw new RuntimeException("android-23 android.jar not found at " + androidJar.absolutePath)
    }
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compileOnly files(androidJar)
    testCompile 'junit:junit:4.12'
}

// turn off strict javadoc
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// dx the output jar which will be used by the agent to load in at runtime.
task jarToDex(dependsOn: 'jar', type: Exec) {
  commandLine '../../../../prebuilts/sdk/tools/dx', "--dex",
    "--output=" + libsDir.absolutePath + "/agentlib.jar",
    libsDir.absolutePath + "/" + archivesBaseName + ".jar"
}
assemble.dependsOn(jarToDex)
